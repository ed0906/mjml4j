apply plugin: 'maven'
apply plugin: 'signing'

version = VERSION
String group = GROUP_ID

String pomName = ARTIFACT_ID
String pomRepositoryUrl = PROJECT_URL
String pomScmUrl = SCM_URL
String pomDescription = PROJECT_DESCRIPTION
String pomLicenseName = LICENSE_NAME
String pomLicenseUrl = LICENSE_URL
String pomDeveloperId = DEVELOPER_ID
String pomDeveloperEmail = DEVELOPER_EMAIL
String pomDeveloperName = DEVELOPER_NAME

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: OSSRH_USERNAME, password: OSSRH_PASSWORD)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: OSSRH_USERNAME, password: OSSRH_PASSWORD)
            }

            pom.project {
                name pomName
                description pomDescription
                url pomRepositoryUrl
                version version
                groupId group
                artifactId pomName
                packaging 'jar'
                scm {
                    connection pomScmUrl
                    developerConnection pomScmUrl
                    url pomScmUrl
                }
                licenses {
                    license {
                        name pomLicenseName
                        url pomLicenseUrl
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id pomDeveloperId
                        name pomDeveloperName
                        email pomDeveloperEmail
                    }
                }
            }
        }
    }
}
